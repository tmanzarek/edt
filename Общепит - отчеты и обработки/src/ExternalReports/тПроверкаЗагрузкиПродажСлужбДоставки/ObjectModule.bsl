// Тимур - старт - 04.03.2020

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка	= Ложь;
	ДокументРезультат.Очистить();
	
	Настройки				= КомпоновщикНастроек.ПолучитьНастройки();
	
	СхемаКомпоновкиДанных	= ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	КомпоновщикМакета		= Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки			= КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	ПроцессорКомпоновки		= Новый ПроцессорКомпоновкиДанных;
	
	ПериодОтчета			= Настройки.ПараметрыДанных.Элементы[0].Значение;
	
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, Новый Структура("ЗагруженныеПродажи", ЗагруженныеПродажи(ПериодОтчета.ДатаНачала, ПериодОтчета.ДатаОкончания)), ДанныеРасшифровки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид",				"ДополнительныйОтчет");
	//Варианты: "ДополнительнаяОбработка", "ДополнительныйОтчет", "ЗаполнениеОбъекта", "Отчет", "ПечатнаяФорма", "СозданиеСвязанныхОбъектов"
	
	ПараметрыРегистрации.Вставить("Наименование",		"Проверка продаж служб доставки");
	ПараметрыРегистрации.Вставить("Версия",				"1.01");
	ПараметрыРегистрации.Вставить("БезопасныйРежим",	Истина); //Варианты: Истина, Ложь
	ПараметрыРегистрации.Вставить("Информация",			"Проверка продаж служб доставки");
	
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	
	ДобавитьКоманду(ТаблицаКоманд,
		"Проверка продаж служб доставки",
		"ПроверкаЗагрузкиПродажСлужбДоставки",
		"ОткрытиеФормы", //Использование. Варианты: "ОткрытиеФормы", "ВызовКлиентскогоМетода", "ВызовСерверногоМетода"
		Ложь,//Показывать оповещение. Варианты Истина, Ложь 
		"");//Модификатор 
	
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление",			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор",			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование",			Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение",	Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор",				Новый ОписаниеТипов("Строка"));
	
	Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда						= ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление			= Представление;
	НоваяКоманда.Идентификатор			= Идентификатор;
	НоваяКоманда.Использование			= Использование;
	НоваяКоманда.ПоказыватьОповещение	= ПоказыватьОповещение;
	НоваяКоманда.Модификатор			= Модификатор;
	
КонецПроцедуры

Функция ЗагруженныеПродажи(ДатаНачала = Неопределено, ДатаОкончания = Неопределено)
	Результат	= ИнициализироватьТЗ();
	Запрос		= Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументыПродажи.Ссылка КАК РозничнаяПродажа,
	|	ДокументыПродажи.СуммаДокумента КАК СуммаПоДанным1С,
	|	ПОДСТРОКА(ДокументыПродажи.Комментарий, 0, 1000) КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(ДокументыПродажи.Комментарий, 0, 1000) ПОДОБНО &ПризнакДеливериКлаб
	|			ТОГДА ""Деливери клаб""
	|		ИНАЧЕ ""Яндекс.Еда""
	|	КОНЕЦ КАК НаименованиеСлужбыДоставки
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокументыПродажи
	|ГДЕ
	// Тимур - старт - 23.03.2020
	//|	ДокументыПродажи.Проведен
	//|	И ДокументыПродажи.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	ДокументыПродажи.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	// финиш - 23.03.2020
	|	И ДокументыПродажи.Комментарий ПОДОБНО &ПризнакСлужбыДоставки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыПродажи.Дата");
	Запрос.УстановитьПараметр("ДатаНачала",				?(ЗначениеЗаполнено(ДатаНачала),	НачалоДня(ДатаНачала), Дата(2019,01,01)));
	Запрос.УстановитьПараметр("ДатаОкончания",			?(ЗначениеЗаполнено(ДатаОкончания),	КонецДня(ДатаОкончания), КонецДня(ТекущаяДата())));
	Запрос.УстановитьПараметр("ПризнакСлужбыДоставки",	"%Код заказа%");
	Запрос.УстановитьПараметр("ПризнакДеливериКлаб",	"%адрес доставки%");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаТЗ								= Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЗ, Выборка);
			СтрокаТЗ.СуммаПоДаннымСлужбыДоставки	= ПолучитьСуммуПоДаннымСлужбыДоставки(Выборка.Комментарий);
		КонецЦикла;
	КонецЕсли;
	Возврат Результат;
КонецФункции // ЗагруженныеПродажи()

Функция ИнициализироватьТЗ()
	ТЗ		= Новый ТаблицаЗначений;
	Колонки	= ТЗ.Колонки;
	Колонки.Добавить("РозничнаяПродажа");
	Колонки.Добавить("СуммаПоДанным1С");
	Колонки.Добавить("СуммаПоДаннымСлужбыДоставки");
	Колонки.Добавить("НаименованиеСлужбыДоставки");
	Возврат ТЗ;
КонецФункции // Результат()

Функция ПолучитьСуммуПоДаннымСлужбыДоставки(Комментарий)
	ПозицияНачала		= СтрНайти(Комментарий, "сумма заказа - ") + 15;
	ПозицияОкончания	= СтрНайти(Комментарий, " руб.");
	Попытка
		Сумма			= Число(Сред(Комментарий, ПозицияНачала, ПозицияОкончания - ПозицияНачала));
	Исключение
		Сумма	= 0;
	КонецПопытки;
	Возврат Сумма;
КонецФункции // ПолучитьСуммуПоДаннымСлужбыДоставки()

#КонецОбласти

// финиш - 04.03.2020