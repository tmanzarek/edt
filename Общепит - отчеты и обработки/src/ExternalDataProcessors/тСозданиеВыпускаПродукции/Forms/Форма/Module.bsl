// Тимур - старт - 16.03.2020

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПродажи(Команда)
	
	Если Объект.Продажи.Количество() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПродажиЗавершение", ЭтотОбъект),
			"Перед заполнением продаж табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПродажиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВыпускПродукции(Команда)
	СоздатьВыпускПродукцииНаСервере(Объект.Продажи);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПродажиЗавершение(РезультатВопроса, ПараметрыЗаписи) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Продажи	= Объект.Продажи;
		Продажи.Очистить();
		ЗаполнитьПродажиНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПродажиНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК ДокументПродажи
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Проведен
	|	И ОтчетОРозничныхПродажах.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтчетОРозничныхПродажах.Дата");
	
	Период = Объект.Период;
	Запрос.УстановитьПараметр("ДатаНачала",		Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",	Период.ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= "В выбранном периоде нет документов продажи.";
		Сообщение.Сообщить();
	Иначе
		Продажи	= Объект.Продажи;
		Выборка	= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Продажи.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьВыпускПродукцииНаСервере(Знач Продажи)
	
	ПодразделенияМенеджер	= Справочники.ПодразделенияОрганизаций;
	
	Для каждого СтрокаТЧ Из Продажи Цикл
		
		ДокументПродажи			= СтрокаТЧ.ДокументПродажи;
		ВыпускПродукцииОбъект	= Документы.ОбщепитВыпускПродукции.СоздатьДокумент();
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ВыпускПродукцииОбъект, ДокументПродажи, Истина); // Копировать подразделение = Истина
		ВыпускПродукцииОбъект.ВидОперации					= Перечисления.ОбщепитВидыОперацийВыпускПродукции.ВыпускПродукции;
		ВыпускПродукцииОбъект.ДокументОснование				= ДокументПродажи;
		ВыпускПродукцииОбъект.ГотовитьМодификаторы			= Ложь;
		ВыпускПродукцииОбъект.МодификаторыКакИнгредиенты	= Ложь;
		
		ВыпускПродукцииОбъект.СкопироватьТовары(ДокументПродажи, Истина, Истина);
		ВыпускПродукцииОбъект.СкопироватьМодификаторы(ДокументПродажи, Истина);
		
		// Поставим выпуск раньше реализации
		ВыпускПродукцииОбъект.Дата = ДокументПродажи.Дата - 1;
		
		ВыпускПродукцииОбъект.ДляСписанияНДСиспользоватьСчетИАналитикуУчетаЗатрат = Истина;
		ЗаполнениеДокументов.Заполнить(ВыпускПродукцииОбъект, ДокументПродажи, Ложь);
		
		Склад								= ВыпускПродукцииОбъект.Склад;
		Если ЗначениеЗаполнено(Склад) И НЕ ЗначениеЗаполнено(ВыпускПродукцииОбъект.ТипЦен) И ЗначениеЗаполнено(Склад.ТипЦенРозничнойТорговли) Тогда
			ВыпускПродукцииОбъект.ТипЦен	= Склад.ТипЦенРозничнойТорговли;
		КонецЕсли;
		
		Если ВыпускПродукцииОбъект.ПодразделениеОрганизации.Пустая() Тогда
			ВыпускПродукцииОбъект.ПодразделениеОрганизации = ПодразделенияМенеджер.НайтиПоНаименованию(Склад.Наименование);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыпускПродукцииОбъект.СтатьяЗатратПроизводство) Тогда
			ВыпускПродукцииОбъект.СтатьяЗатратПроизводство = Константы.ОбщепитСтатьяЗатратПроизводство.Получить();
		КонецЕсли;
		
		ВыпускПродукцииОбъект.НеИспользоватьСезонныеПроценты	= Константы.ОбщепитНеИспользоватьСезонныеПроценты.Получить();
		ВыпускПродукцииОбъект.РазрешитьНедовложения				= Истина;
		ВыпускПродукцииОбъект.РежимИспользованияАналогов		= Перечисления.ОбщепитРежимыИспользованияАналогов.Разрешить;
		ВыпускПродукцииОбъект.ВидПроизводства					= Перечисления.ОбщепитВидыПроизводства.ПеределыОтдельнойОперацией;
		
		Попытка
			ВыпускПродукцииОбъект.Записать(РежимЗаписиДокумента.Проведение);
			ТекстСообщения	= "По документу " + ДокументПродажи + " создан " + ВыпускПродукцииОбъект;
		Исключение
			ТекстСообщения	= "По документу " + ДокументПродажи + " НЕ создан документ выпуска продукции: " + ОписаниеОшибки();
		КонецПопытки;
		
		Сообщение		= Новый СообщениеПользователю;
		Сообщение.Текст	= ТекстСообщения;
		Сообщение.Сообщить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// финиш - 16.03.2020
