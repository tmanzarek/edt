// Тимур - старт - 14.05.2020

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере(Объект.ДатаНачала, Объект.ДатаОкончания);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьДокументыНаСервере(ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос(ТекстЗапроса());
	
	Субконто = Новый Массив;
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии);
	Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Субконто",		Субконто);
	
	Организация				= Справочники.Организации.ОрганизацияПоУмолчанию();
	ТекущаяДата				= ТекущаяДата();
	Руководители			= ОтветственныеЛицаБП.ОтветственныеЛица(Организация, ТекущаяДата);

	ОбщиеСвойстваДокумента	= Новый Структура("ВидОперации,Организация,ВалютаДокумента,КурсДокумента,КратностьДокумента,ТипЦен,СуммаВключаетНДС,РежимРасчетаСписанияВПроизводство,
		|РежимИспользованияАналогов,ВидПроизводства,РазрешитьНедовложения,СтатьяЗатратПроизводство,ДляСписанияНДСиспользоватьСчетИАналитикуУчетаЗатрат,СтатьяДвиженияДенежныхСредств,
		|ОбщепитСпособРасчетаЦеныРеализацииПродукции,СпособЗачетаАвансов,БанковскийСчетОрганизации,Ответственный,Руководитель,ДокументБезНДС,Комментарий",
		Перечисления.ОбщепитВидыОперацийВыпускПродукции.ВыпускПродукции, Организация, Константы.ВалютаРегламентированногоУчета.Получить(), 1, 1,
		Справочники.ТипыЦенНоменклатуры.РозничнаяЦена, Ложь, Перечисления.ОбщепитРежимыРасчетаСписанияВПроизводство.ПоНорме, Перечисления.ОбщепитРежимыИспользованияАналогов.Разрешить,
		Перечисления.ОбщепитВидыПроизводства.ПеределыОтдельнойОперацией, Истина, Справочники.СтатьиЗатрат.СписаниеМатериалов, Истина, Справочники.СтатьиДвиженияДенежныхСредств.РозничнаяВыручка,
		Перечисления.ОбщепитСпособРасчетаЦеныРеализацииПродукции.БезИзменения, Перечисления.СпособыЗачетаАвансов.Автоматически, Организация.ОсновнойБанковскийСчет, ПараметрыСеанса.ТекущийПользователь,
		Руководители.Руководитель, Истина, "" + ТекущаяДата + " - создан автоматически по отрицательным остаткам модификаторов");
	
	СчетчикЗагруженныхДокументов	= 0;
	ВыпускПродукцииМенеджер			= Документы.ОбщепитВыпускПродукции;
	
	// Тимур - старт - 21 мая 2020 г.
	ТЗПредыдущийОстаток	= Новый ТаблицаЗначений();
	ТЗПредыдущийОстаток.Колонки.Добавить("Склад");
	ТЗПредыдущийОстаток.Колонки.Добавить("Номенклатура");
	ТЗПредыдущийОстаток.Колонки.Добавить("Остаток");
	
	Отбор				= Новый Структура("Склад,Номенклатура");
	// финиш - 21 мая 2020 г.
	
	ВыборкаПоДням = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоДням.Следующий() Цикл

		ВыборкаПоСкладам	= ВыборкаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаПоСкладам.Следующий() Цикл
	
			ВыпускПродукцииОбъект							= ВыпускПродукцииМенеджер.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(ВыпускПродукцииОбъект, ОбщиеСвойстваДокумента);
			ВыпускПродукцииОбъект.Дата						= ВыборкаПоДням.Дата;
			ВыпускПродукцииОбъект.УстановитьНовыйНомер();
			Склад											= ВыборкаПоСкладам.Склад;
			ВыпускПродукцииОбъект.Склад						= Склад;
			ВыпускПродукцииОбъект.ПодразделениеОрганизации	= Справочники.ПодразделенияОрганизаций.НайтиПоНаименованию(Строка(Склад));

			Товары = ВыпускПродукцииОбъект.Товары;

			ВыборкаПоНоменклатуре	= ВыборкаПоСкладам.Выбрать();

			Пока ВыборкаПоНоменклатуре.Следующий() Цикл
				
				// Тимур - старт - 21 мая 2020 г.
				//ЗаполнитьЗначенияСвойств(Товары.Добавить(), ВыборкаПоНоменклатуре);
				
				ЗаполнитьЗначенияСвойств(Отбор, ВыборкаПоНоменклатуре);
				МассивПредОстаток	= ТЗПредыдущийОстаток.НайтиСтроки(Отбор);
				
				Если МассивПредОстаток.Количество() Тогда
					СтрокаПредОстаток	= МассивПредОстаток[0];
				Иначе
					СтрокаПредОстаток			= ТЗПредыдущийОстаток.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаПредОстаток, Отбор);
					СтрокаПредОстаток.Остаток	= 0;
				КонецЕсли;
				ПредОстаток				= СтрокаПредОстаток.Остаток; 
				
				НоваяСтрока					= Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоНоменклатуре);
				НоваяСтрока.Количество		= НоваяСтрока.Количество - ПредОстаток;
				
				СтрокаПредОстаток.Остаток	= ВыборкаПоНоменклатуре.Количество;
				  
				// финиш - 21 мая 2020 г.
			КонецЦикла;

			Попытка
				ВыпускПродукцииОбъект.Записать(РежимЗаписиДокумента.Запись);
				СчетчикЗагруженныхДокументов = СчетчикЗагруженныхДокументов + 1;
				ВыпускПродукцииОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Сообщить("Ошибка при создании документа " + ВыпускПродукцииОбъект + ": " + ОписаниеОшибки(),
					СтатусСообщения.Важное);
			КонецПопытки;

		КонецЦикла;
		
	КонецЦикла;
	
	Сообщить("Создано документов: " + СчетчикЗагруженныхДокументов, СтатусСообщения.Информация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапроса()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Период КАК Дата,
	|	ВЫРАЗИТЬ(Субконто3 КАК Справочник.Склады) КАК Склад,
	|	ВЫРАЗИТЬ(Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Субконто1 КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	-ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстатокДт КАК Количество,
	|	1 КАК Коэффициент,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция) КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД) КАК СчетДоходов,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД) КАК СчетРасходов,
	|	ВЫРАЗИТЬ(Субконто1 КАК Справочник.Номенклатура).НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ Блюда
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, День,,
	|		Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция), &Субконто, ВЫРАЗИТЬ(Субконто1 КАК
	|		Справочник.Номенклатура) В
	|		(ВЫБРАТЬ
	|			ОбщепитМодификаторы.ДопНоменклатура
	|		ИЗ
	|			Справочник.ОбщепитМодификаторы КАК ОбщепитМодификаторы
	|		ГДЕ
	|			ОбщепитМодификаторы.ДопНоменклатура.ОбщепитВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ОбщепитВидыНоменклатуры.Блюдо))) КАК
	|		ХозрасчетныйОстаткиИОбороты
	|ГДЕ
	|	ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстатокДт < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Блюда.Дата КАК Дата,
	|	Блюда.Склад КАК Склад,
	|	Блюда.Номенклатура КАК Номенклатура,
	|	Блюда.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Блюда.Количество КАК Количество,
	|	Блюда.Коэффициент КАК Коэффициент,
	|	Блюда.СчетУчета КАК СчетУчета,
	|	ЕСТЬNULL(ОбщепитРецептура.Ссылка, ЗНАЧЕНИЕ(Документ.ОбщепитРецептура.ПустаяСсылка)) КАК Рецептура,
	|	Блюда.СчетДоходов КАК СчетДоходов,
	|	Блюда.НоменклатурнаяГруппа КАК Субконто,
	|	Блюда.СчетРасходов КАК СчетРасходов
	|ИЗ
	|	Блюда КАК Блюда
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбщепитРецептура КАК ОбщепитРецептура
	|		ПО Блюда.Номенклатура = ОбщепитРецептура.Номенклатура
	|		И (ОбщепитРецептура.Основная)
	|УПОРЯДОЧИТЬ ПО
	|	Блюда.Дата,
	|	Блюда.Номенклатура.Наименование,
	|	Блюда.Количество
	|ИТОГИ
	|	МИНИМУМ(ЕдиницаИзмерения),
	|	СУММА(Количество),
	|	МИНИМУМ(Коэффициент),
	|	МИНИМУМ(СчетУчета),
	|	МИНИМУМ(Рецептура),
	|	МИНИМУМ(СчетДоходов),
	|	МИНИМУМ(Субконто),
	|	МИНИМУМ(СчетРасходов)
	|ПО
	|	Дата,
	|	Склад";
	Возврат ТекстЗапроса;
КонецФункции // ()

// финиш - 14.05.2020
