// Тимур - старт - 15.04.2020

&НаСервереБезКонтекста
Процедура ИсправитьДокументыНаСервере(ДатаНачала, ДатаОкончания, Ответственный)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровУслуг.Представление КАК Представление
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
	|ГДЕ
	|	ПоступлениеТоваровУслуг.Ответственный <> &Ответственный
	|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоступлениеТоваровУслуг.Дата,
	|	ПоступлениеТоваровУслуг.Номер");
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	Запрос.УстановитьПараметр("Ответственный",	Ответственный);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Товар					= Перечисления.ОбщепитВидыНоменклатуры.Товар;
	Блюдо					= Перечисления.ОбщепитВидыНоменклатуры.Блюдо;
	ТехническийИнгредиент	= Перечисления.ОбщепитВидыНоменклатуры.ТехнологическийОтход;
	Материал				= Перечисления.ОбщепитВидыНоменклатуры.Материал;
	
	ПланСчетовМенеджер		= ПланыСчетов.Хозрасчетный;
	Счет43					= ПланСчетовМенеджер.ГотоваяПродукция;
	Счет41_01				= ПланСчетовМенеджер.ТоварыНаСкладах;
	Счет10_01				= ПланСчетовМенеджер.СырьеИМатериалы;
	Счет10_09				= ПланСчетовМенеджер.ИнвентарьИХозяйственныеПринадлежности;
	
	НоменклатураМенеджер	= Справочники.Номенклатура;
	Шоколадки				= НоменклатураМенеджер.НайтиПоКоду("00-00000062");
	ХолодныеНапитки			= НоменклатураМенеджер.НайтиПоКоду("00-00000054");
	// Тимур - старт - 19 мая 2020 г.
	Добавки					= НоменклатураМенеджер.НайтиПоКоду("00-00000092");
	// финиш - 19 мая 2020 г.
	// Тимур - старт - 7 июл. 2020 г.
	Прочее					= НоменклатураМенеджер.НайтиПоКоду("00-00000093");
	// финиш - 7 июл. 2020 г.
	
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			
			Номенклатура	= СтрокаТЧ.Номенклатура;
			ВидНоменклатуры	= Номенклатура.ОбщепитВидНоменклатуры;
			Родитель		= Номенклатура.Родитель;
			
			Если ВидНоменклатуры = Блюдо Тогда
				СтрокаТЧ.СчетУчета = Счет43;
			ИначеЕсли ВидНоменклатуры = ТехническийИнгредиент Тогда
				СтрокаТЧ.СчетУчета = Счет10_01;
			ИначеЕсли ВидНоменклатуры = Материал Тогда
				СтрокаТЧ.СчетУчета = Счет10_09;
			ИначеЕсли ВидНоменклатуры = Товар Тогда
				Если Родитель = Шоколадки Тогда
					СтрокаТЧ.СчетУчета = Счет41_01;
				ИначеЕсли Родитель = ХолодныеНапитки Тогда
					СтрокаТЧ.СчетУчета = Счет41_01;
				// Тимур - старт - 19 мая 2020 г.
				ИначеЕсли Родитель = Добавки Тогда
					СтрокаТЧ.СчетУчета = Счет41_01;
				// финиш - 19 мая 2020 г.
				// Тимур - старт - 7 июл. 2020 г.
				ИначеЕсли Родитель = Прочее Тогда
					СтрокаТЧ.СчетУчета = Счет41_01;
				// финиш - 7 июл. 2020 г.
				Иначе
					СтрокаТЧ.СчетУчета = Счет10_01;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента[?(ДокументОбъект.Проведен, "Проведение", "Запись")]);
		Исключение
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Ошибка записи документа " + Выборка.Представление + ": " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
		
	КонецЦикла;
	
	Сообщение		= Новый СообщениеПользователю;
	Сообщение.Текст	= "Обработка документов завершена.";
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьДокументы(Команда)
	ИсправитьДокументыНаСервере(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Ответственный);
КонецПроцедуры

// финиш - 15.04.2020
