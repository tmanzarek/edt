// Тимур - старт - 16.03.2020
&НаСервереБезКонтекста
Процедура ДозаполнитьНаСервере(ДатаНачала, ДатаОкончания, СчетУчетаБлюдо, СчетУчетаТовар, СчетДоходов, СчетРасходов)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Представление КАК Представление
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Дата >= &ДатаНачала
	|	И ОтчетОРозничныхПродажах.Дата <= &ДатаОкончания");
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗаполнятьСчетУчетаБлюдо			= НЕ СчетУчетаБлюдо.Пустая();
	ЗаполнятьСчетУчетаТовар			= НЕ СчетУчетаТовар.Пустая();
	ЗаполнятьСчетДоходов			= НЕ СчетДоходов.Пустая();
	ЗаполнятьСчетРасходов			= НЕ СчетРасходов.Пустая();
	
	ПустойСчет						= ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	Товар							= Перечисления.ОбщепитВидыНоменклатуры.Товар;
	Блюдо							= Перечисления.ОбщепитВидыНоменклатуры.Блюдо;
	
	// Тимур - старт - 15.01.2020
	Штука = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("870");
	// финиш - 15.01.2020
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Для каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			
			ВидНоменклатуры				= СтрокаТЧ.Номенклатура.ОбщепитВидНоменклатуры;
			Если ЗаполнятьСчетУчетаБлюдо И ВидНоменклатуры = Блюдо Тогда
				СтрокаТЧ.СчетУчета		= СчетУчетаБлюдо;
			ИначеЕсли ЗаполнятьСчетУчетаТовар И ВидНоменклатуры = Товар Тогда
				СтрокаТЧ.СчетУчета		= СчетУчетаТовар;
			КонецЕсли;
			Если ЗаполнятьСчетДоходов Тогда
				СтрокаТЧ.СчетДоходов	= СчетДоходов;
			КонецЕсли;
			Если ЗаполнятьСчетРасходов Тогда
				СтрокаТЧ.СчетРасходов	= СчетРасходов;
			КонецЕсли;
			
			// Тимур - старт - 15.01.2020
			СтрокаТЧ.ЕдиницаИзмерения	= Штука;
			СтрокаТЧ.Коэффициент		= 1;
			// финиш - 15.01.2020
			
		КонецЦикла;
		
		Для каждого СтрокаТЧ Из ДокументОбъект.Возвраты Цикл
			
			ВидНоменклатуры				= СтрокаТЧ.Номенклатура.ОбщепитВидНоменклатуры;
			Если ЗаполнятьСчетУчетаБлюдо И ВидНоменклатуры = Блюдо Тогда
				СтрокаТЧ.СчетУчета		= СчетУчетаБлюдо;
			ИначеЕсли ЗаполнятьСчетУчетаТовар И ВидНоменклатуры = Товар Тогда
				СтрокаТЧ.СчетУчета		= СчетУчетаТовар;
			КонецЕсли;
			Если ЗаполнятьСчетДоходов Тогда
				СтрокаТЧ.СчетДоходов	= СчетДоходов;
			КонецЕсли;
			Если ЗаполнятьСчетРасходов Тогда
				СтрокаТЧ.СчетРасходов	= СчетРасходов;
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента[?(ДокументОбъект.Проведен, "Проведение", "Запись")]);
		Исключение
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Ошибка записи документа " + Выборка.Представление + ": " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
		
	КонецЦикла;
	
	Сообщение		= Новый СообщениеПользователю;
	Сообщение.Текст	= "Обработка документов завершена.";
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаКлиенте
Процедура Дозаполнить(Команда)
	ДозаполнитьНаСервере(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.СчетУчетаБлюдо, Объект.СчетУчетаТовар, Объект.СчетДоходов, Объект.СчетРасходов);
КонецПроцедуры

// финиш - 16.03.2020
