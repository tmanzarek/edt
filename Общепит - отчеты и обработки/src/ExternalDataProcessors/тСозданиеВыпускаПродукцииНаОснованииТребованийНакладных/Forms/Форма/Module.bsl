// Тимур - старт - 16.04.2020

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере(Объект.ДатаНачала, Объект.ДатаОкончания);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьДокументыНаСервере(ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос(ТекстЗапроса());
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	
	Организация				= Справочники.Организации.ОрганизацияПоУмолчанию();
	ТекущаяДата				= ТекущаяДата();
	Руководители			= ОтветственныеЛицаБП.ОтветственныеЛица(Организация, ТекущаяДата);

	ОбщиеСвойстваДокумента	= Новый Структура("ВидОперации,Организация,ВалютаДокумента,КурсДокумента,КратностьДокумента,ТипЦен,СуммаВключаетНДС,РежимРасчетаСписанияВПроизводство,
		|РежимИспользованияАналогов,ВидПроизводства,РазрешитьНедовложения,СтатьяЗатратПроизводство,ДляСписанияНДСиспользоватьСчетИАналитикуУчетаЗатрат,СтатьяДвиженияДенежныхСредств,
		|ОбщепитСпособРасчетаЦеныРеализацииПродукции,СпособЗачетаАвансов,БанковскийСчетОрганизации,Ответственный,Руководитель,ДокументБезНДС,Комментарий",
		Перечисления.ОбщепитВидыОперацийВыпускПродукции.ВыпускПродукции, Организация, Константы.ВалютаРегламентированногоУчета.Получить(), 1, 1,
		Справочники.ТипыЦенНоменклатуры.РозничнаяЦена, Ложь, Перечисления.ОбщепитРежимыРасчетаСписанияВПроизводство.ПоНорме, Перечисления.ОбщепитРежимыИспользованияАналогов.Разрешить,
		Перечисления.ОбщепитВидыПроизводства.ПеределыОтдельнойОперацией, Истина, Справочники.СтатьиЗатрат.СписаниеМатериалов, Истина, Справочники.СтатьиДвиженияДенежныхСредств.РозничнаяВыручка,
		Перечисления.ОбщепитСпособРасчетаЦеныРеализацииПродукции.БезИзменения, Перечисления.СпособыЗачетаАвансов.Автоматически, Организация.ОсновнойБанковскийСчет, ПараметрыСеанса.ТекущийПользователь,
		Руководители.Руководитель, Истина, "" + ТекущаяДата + " - создан автоматически на основании требований-накладных");
	
	СчетчикЗагруженныхДокументов	= 0;
	ВыпускПродукцииМенеджер			= Документы.ОбщепитВыпускПродукции;
	
	ВыборкаПоДням = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоДням.Следующий() Цикл
		
		ВыборкаПоСкладам	= ВыборкаПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоСкладам.Следующий() Цикл
			
			ВыпускПродукцииОбъект							= ВыпускПродукцииМенеджер.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(ВыпускПродукцииОбъект, ОбщиеСвойстваДокумента);
			ВыпускПродукцииОбъект.Дата						= ВыборкаПоДням.Дата;
			ВыпускПродукцииОбъект.УстановитьНовыйНомер();
			Склад											= ВыборкаПоСкладам.Склад;
			ВыпускПродукцииОбъект.Склад						= Склад;
			ВыпускПродукцииОбъект.ПодразделениеОрганизации	= Справочники.ПодразделенияОрганизаций.НайтиПоНаименованию(Строка(Склад));
			
			Товары = ВыпускПродукцииОбъект.Товары;
			
			ВыборкаПоНоменклатуре	= ВыборкаПоСкладам.Выбрать();
			
			Пока ВыборкаПоНоменклатуре.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Товары.Добавить(), ВыборкаПоНоменклатуре);
			КонецЦикла;
			
			Попытка
				ВыпускПродукцииОбъект.Записать(РежимЗаписиДокумента.Запись);
				СчетчикЗагруженныхДокументов = СчетчикЗагруженныхДокументов + 1;
				ВыпускПродукцииОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				Сообщить("Ошибка при создании документа " + ВыпускПродукцииОбъект + ": " + ОписаниеОшибки(), СтатусСообщения.Важное);
			КонецПопытки;
			
		КонецЦикла;
	КонецЦикла;
	
	Сообщить("Создано документов: " + СчетчикЗагруженныхДокументов, СтатусСообщения.Информация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапроса()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТребованиеНакладнаяМатериалы.Ссылка.Дата, ДЕНЬ) КАК Дата,
	|	ТребованиеНакладнаяМатериалы.Ссылка.Склад КАК Склад,
	|	ТребованиеНакладнаяМатериалы.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ТребованиеНакладнаяМатериалы.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	СУММА(ТребованиеНакладнаяМатериалы.Количество) КАК Количество,
	|	МИНИМУМ(1) КАК Коэффициент,
	|	МИНИМУМ(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ГотоваяПродукция)) КАК СчетУчета,
	|	МИНИМУМ(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД)) КАК СчетДоходов,
	|	МИНИМУМ(ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД)) КАК СчетРасходов,
	|	ТребованиеНакладнаяМатериалы.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа
	|ПОМЕСТИТЬ Блюда
	|ИЗ
	|	Документ.ТребованиеНакладная.Материалы КАК ТребованиеНакладнаяМатериалы
	|ГДЕ
	|	ТребованиеНакладнаяМатериалы.Ссылка.Проведен
	|	И ТребованиеНакладнаяМатериалы.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТребованиеНакладнаяМатериалы.Номенклатура.ОбщепитВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ОбщепитВидыНоменклатуры.Блюдо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТребованиеНакладнаяМатериалы.Ссылка.Склад,
	|	ТребованиеНакладнаяМатериалы.Номенклатура,
	|	НАЧАЛОПЕРИОДА(ТребованиеНакладнаяМатериалы.Ссылка.Дата, ДЕНЬ),
	|	ТребованиеНакладнаяМатериалы.Номенклатура.НоменклатурнаяГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Блюда.Дата КАК Дата,
	|	Блюда.Склад КАК Склад,
	|	Блюда.Номенклатура КАК Номенклатура,
	|	Блюда.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Блюда.Количество КАК Количество,
	|	Блюда.Коэффициент КАК Коэффициент,
	|	Блюда.СчетУчета КАК СчетУчета,
	|	ЕСТЬNULL(ОбщепитРецептура.Ссылка, ЗНАЧЕНИЕ(Документ.ОбщепитРецептура.ПустаяСсылка)) КАК Рецептура,
	|	Блюда.СчетДоходов КАК СчетДоходов,
	|	Блюда.НоменклатурнаяГруппа КАК Субконто,
	|	Блюда.СчетРасходов КАК СчетРасходов
	|ИЗ
	|	Блюда КАК Блюда
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбщепитРецептура КАК ОбщепитРецептура
	|		ПО Блюда.Номенклатура = ОбщепитРецептура.Номенклатура
	|			И (ОбщепитРецептура.Основная)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Блюда.Дата,
	|	Блюда.Склад.Наименование,
	|	Блюда.Номенклатура.Наименование,
	|	Блюда.Количество
	|ИТОГИ
	|	МИНИМУМ(ЕдиницаИзмерения),
	|	СУММА(Количество),
	|	МИНИМУМ(Коэффициент),
	|	МИНИМУМ(СчетУчета),
	|	МИНИМУМ(Рецептура),
	|	МИНИМУМ(СчетДоходов),
	|	МИНИМУМ(Субконто),
	|	МИНИМУМ(СчетРасходов)
	|ПО
	|	Дата,
	|	Склад";
	Возврат ТекстЗапроса;
КонецФункции // ()

// финиш - 16.04.2020
