// Тимур - старт - 20 мая 2020 г.
&НаКлиенте
Процедура УстановитьГалкиВИнвентаризациях(Команда)
	УстановитьГалкиВИнвентаризацияхНаСервере(Объект.ДатаНачала, Объект.ДатаОкончания);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьГалкиВИнвентаризацияхНаСервере(ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ИнвентаризацияТоваровНаСкладе.Ссылка,
	|	ИнвентаризацияТоваровНаСкладе.Представление
	|ИЗ
	|	Документ.ИнвентаризацияТоваровНаСкладе КАК ИнвентаризацияТоваровНаСкладе
	|ГДЕ
	|	ИнвентаризацияТоваровНаСкладе.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|УПОРЯДОЧИТЬ ПО
	|	ИнвентаризацияТоваровНаСкладе.Дата,
	|	ИнвентаризацияТоваровНаСкладе.Номер");
	
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ДатаОкончания));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	РежимКорректировкиКоличества = Перечисления.ОбщепитРежимКорректировкиКоличестваВИнвентаризации.НеКорректироватьКнижноеКоличество;
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();

		ДокументОбъект.ОбщепитРежимКорректировкиКоличества	= РежимКорректировкиКоличества;
		ДокументОбъект.ОбменДанными.Загрузка				= Истина;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщение		= Новый СообщениеПользователю;
			Сообщение.Текст	= "Ошибка записи документа " + Выборка.Представление + ": " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
		
	КонецЦикла;
	
	Сообщение		= Новый СообщениеПользователю;
	Сообщение.Текст	= "Обработка документов завершена.";
	Сообщение.Сообщить();
	
КонецПроцедуры
// финиш - 20 мая 2020 г.

